import java.text.SimpleDateFormat

apply plugin: 'com.android.application'
apply plugin: 'com.android.databinding'

def getDate() {
    return new SimpleDateFormat("yyyy.MM.dd").format(new Date());
}

Properties props = new Properties()
props.load(new FileInputStream(file("c:\\Android\\Keystore\\ru.p3tr0vich.fuel.properties")))

android {
    signingConfigs {
        debug {
            keyAlias 'ru.p3tr0vich.fuel'
            keyPassword props['keyPassword']
            storeFile file(props['storeFile'])
            storePassword props['storePassword']
        }
        release {
            keyAlias 'ru.p3tr0vich.fuel'
            keyPassword props['keyPassword']
            storeFile file(props['storeFile'])
            storePassword props['storePassword']
        }
    }
    //noinspection GroovyAssignabilityCheck
    compileSdkVersion rootProject.ext.compileSdkVersion
    //noinspection GroovyAssignabilityCheck
    buildToolsVersion rootProject.ext.buildToolsVersion
    defaultConfig {
        applicationId "ru.p3tr0vich.fuel"
        //noinspection GroovyAssignabilityCheck
        minSdkVersion rootProject.ext.minSdkVersion
        //noinspection GroovyAssignabilityCheck
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"
        buildConfigField "String", "BUILD_DATE", "new String(\"" + getDate() + "\")"

        resConfigs "ru"
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            //noinspection GroovyAssignabilityCheck
            signingConfig signingConfigs.debug

            ext.enableCrashlytics = false
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            //noinspection GroovyAssignabilityCheck
            signingConfig signingConfigs.release

            applicationVariants.all { variant ->
                appendVersionName(variant, defaultConfig)
            }

            ext.enableCrashlytics = false
        }
    }

    productFlavors {
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude '.readme'
    }

    dataBinding {
        enabled = true
    }
}

@SuppressWarnings("GroovyAssignabilityCheck")
def appendVersionName(variant, defaultConfig) {
    variant.outputs.each { output ->
        if (output.zipAlign) {
            def file = output.outputFile
            def fileName = file.name.replace("app-release.apk", defaultConfig.applicationId + "-" +
                    defaultConfig.versionName + "-release.apk")
            output.outputFile = new File(file.parent, fileName)
        }

        def file = output.packageApplication.outputFile
        def fileName = file.name.replace("app-release.apk", defaultConfig.applicationId + "-" +
                defaultConfig.versionName + "-release.apk")
        output.packageApplication.outputFile = new File(file.parent, fileName)
    }
}

repositories {
    maven { url "https://jitpack.io" }
}

dependencies {
    //noinspection GroovyAssignabilityCheck
    compile support_appcompat
    //noinspection GroovyAssignabilityCheck
    compile support_design
    //noinspection GroovyAssignabilityCheck
    compile support_preference7
    //noinspection GroovyAssignabilityCheck
    compile support_preference14
    //noinspection GroovyAssignabilityCheck
    compile support_cardview
    //noinspection GroovyAssignabilityCheck
    compile support_recyclerview

    compile 'com.github.PhilJay:MPAndroidChart:v2.1.6'

    compile project(':libs:fab')
    compile project(':libs:materialish-progress')
    compile project(':libs:materialdatetimepicker')
}
