apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

buildscript {
    ext {
        keystorePath = "D:\\android\\keystore"
    }
}

Properties props = new Properties()
props.load(new FileInputStream(file("$keystorePath\\ru.p3tr0vich.fuel.properties")))

android {
    signingConfigs {
        debug {
            keyAlias 'ru.p3tr0vich.fuel'
            keyPassword props['keyPassword']
            storeFile file("$keystorePath\\keystore.jks")
            storePassword props['storePassword']
        }

        release {
            keyAlias 'ru.p3tr0vich.fuel'
            keyPassword props['keyPassword']
            storeFile file("$keystorePath\\keystore.jks")
            storePassword props['storePassword']
        }
    }

    compileSdkVersion rootProject.ext.compileSdkVer
    buildToolsVersion rootProject.ext.buildToolsVer

    defaultConfig {
        applicationId "ru.p3tr0vich.fuel"

        minSdkVersion rootProject.ext.minSdkVer
        targetSdkVersion rootProject.ext.targetSdkVer

        versionCode 30
        versionName "3.0"

        buildConfigField "long", "BUILD_DATE", System.currentTimeMillis() + "l"

        buildConfigField "String", "YANDEX_DISK_CLIENT_ID", "\"${props['yandexDiskClientId']}\""
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            signingConfig signingConfigs.debug

            ext.enableCrashlytics = false
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            signingConfig signingConfigs.release

            applicationVariants.all { variant ->
                appendVersionName(variant, defaultConfig)
            }

            ext.enableCrashlytics = false
        }
    }

    productFlavors {
    }

    //noinspection GroovyMissingReturnStatement
    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude '.readme'
    }

    dataBinding {
        enabled = true
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    androidExtensions {
        experimental = true
    }

    kapt {
        generateStubs = true
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }
}

afterEvaluate {
    android.sourceSets.all { sourceSet ->
        if (!sourceSet.name.startsWith("test")) {
            sourceSet.kotlin.setSrcDirs([])
        }
    }
}

static def appendVersionName(variant, defaultConfig) {
    variant.outputs.all { output ->
        outputFileName = "${defaultConfig.applicationId}-${variant.versionName}-${variant.name}.apk"
    }
}

dependencies {
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.1.0-beta01'
    implementation 'androidx.preference:preference:1.0.0'
    implementation 'androidx.legacy:legacy-preference-v14:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'

    implementation 'com.google.android.material:material:1.1.0-alpha07'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVer"

    implementation "com.github.P3tr0viCh:ExpansionPanel:1.0.0"

    implementation "com.google.android.gms:play-services-location:17.0.0"

    implementation "com.github.PhilJay:MPAndroidChart:v3.1.0-alpha"
    implementation "com.yandex.android:disk-restapi-sdk:1.03"

    implementation project(':libs:fab')
    implementation project(':libs:materialish-progress')
    implementation project(':libs:materialdatetimepicker')

    testImplementation 'junit:junit:4.12'
}

repositories {
    mavenCentral()
}