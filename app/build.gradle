apply plugin: 'com.android.application'

Properties props = new Properties()
props.load(new FileInputStream(file("c:\\Android\\Keystore\\ru.p3tr0vich.fuel.properties")))

android {
    signingConfigs {
        debug {
            keyAlias 'ru.p3tr0vich.fuel'
            keyPassword props['keyPassword']
            storeFile file(props['storeFile'])
            storePassword props['storePassword']
        }
        release {
            keyAlias 'ru.p3tr0vich.fuel'
            keyPassword props['keyPassword']
            storeFile file(props['storeFile'])
            storePassword props['storePassword']
        }
    }

    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    //noinspection GroovyMissingReturnStatement
    defaultConfig {
        applicationId "ru.p3tr0vich.fuel"

        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion

        versionCode 1
        versionName "1.0"

        buildConfigField "long", "BUILD_DATE", System.currentTimeMillis() + "l"
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            signingConfig signingConfigs.debug

            ext.enableCrashlytics = false
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            signingConfig signingConfigs.release

            applicationVariants.all { variant ->
                appendVersionName(variant, defaultConfig)
            }

            ext.enableCrashlytics = false
        }
    }

    productFlavors {
    }

    //noinspection GroovyMissingReturnStatement
    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude '.readme'
    }

    dataBinding {
        enabled = true
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
}

def appendVersionName(variant, defaultConfig) {
    variant.outputs.each { output ->
        if (output.zipAlign) {
            def file = output.outputFile
            def fileName = file.name.replace("app-release.apk", defaultConfig.applicationId + "-" +
                    defaultConfig.versionName + "-release.apk")
            output.outputFile = new File(file.parent, fileName)
        }

        def file = output.packageApplication.outputFile
        def fileName = file.name.replace("app-release.apk", defaultConfig.applicationId + "-" +
                defaultConfig.versionName + "-release.apk")
        output.packageApplication.outputFile = new File(file.parent, fileName)
    }
}

repositories {
    maven { url "https://jitpack.io" }
}

dependencies {
    compile rootProject.ext.supportAppCompat
    compile rootProject.ext.supportDesign
    compile rootProject.ext.supportPreferenceV7
    compile rootProject.ext.supportPreferenceV14
    compile rootProject.ext.supportCardView
    compile rootProject.ext.supportRecyclerView

    compile 'com.github.P3tr0viCh:ExpansionPanel:1.0.0'

    compile 'com.google.android.gms:play-services-location:9.2.1'

    compile 'com.github.PhilJay:MPAndroidChart:v2.1.6'
    compile 'com.yandex.android:disk-restapi-sdk:1.03'

    compile project(':libs:fab')
    compile project(':libs:materialish-progress')
    compile project(':libs:materialdatetimepicker')

    testCompile 'junit:junit:4.12'
}