plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
}

ext {
    keystorePath = "y:\\Документы\\keystore"
}

Properties props = new Properties()
props.load(new FileInputStream(file("$keystorePath\\ru.p3tr0vich.fuel.properties")))

android {
    namespace "ru.p3tr0vich.fuel"

    defaultConfig {
        applicationId "ru.p3tr0vich.fuel"

        compileSdk rootProject.ext.compileSdkVersion

        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion

        versionCode 33
        versionName "3.3"

        buildConfigField "long", "BUILD_DATE", System.currentTimeMillis() + "l"

        buildConfigField "String", "YANDEX_DISK_CLIENT_ID", "\"${props['yandexDiskClientId']}\""
    }

    signingConfigs {
        debug {
            keyAlias 'ru.p3tr0vich.fuel'
            keyPassword props['keyPassword']
            storeFile file("$keystorePath\\keystore.jks")
            storePassword props['storePassword']
        }

        release {
            keyAlias 'ru.p3tr0vich.fuel'
            keyPassword props['keyPassword']
            storeFile file("$keystorePath\\keystore.jks")
            storePassword props['storePassword']
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            signingConfig signingConfigs.debug

            ext.enableCrashlytics = false
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            signingConfig signingConfigs.release

            applicationVariants.all { variant ->
                appendVersionName(variant, defaultConfig)
            }

            ext.enableCrashlytics = false
        }
    }

    productFlavors {
    }
    packagingOptions {
        resources {
            excludes += ['META-INF/LICENSE.txt', 'META-INF/NOTICE.txt', '.readme']
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    buildFeatures {
        viewBinding true
        dataBinding true
        buildConfig true
    }

    testOptions {
        unitTests.returnDefaultValues true
    }


    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }

    lint {
        abortOnError false
        checkReleaseBuilds false
    }
}

afterEvaluate {
    android.sourceSets.configureEach { sourceSet ->
        if (!sourceSet.name.startsWith("test")) {
            sourceSet.kotlin.setSrcDirs([])
        }
    }
}

static def appendVersionName(variant, defaultConfig) {
    variant.outputs.all { output ->
        outputFileName = "${defaultConfig.applicationId}-${variant.versionName}.apk"
    }
}

dependencies {
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.preference:preference-ktx:1.2.1'
    implementation 'androidx.legacy:legacy-preference-v14:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.3.2'

    implementation 'com.google.android.material:material:1.12.0'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${kotlinVersion}"

    implementation "com.github.P3tr0viCh:ExpansionPanel:2.1.0"

    implementation "com.google.android.gms:play-services-location:21.3.0"

    implementation "com.github.PhilJay:MPAndroidChart:v3.1.0"

    implementation project(':libs:disk-restapi-sdk')

    testImplementation 'junit:junit:4.13.2'
}

repositories {
    mavenCentral()
}